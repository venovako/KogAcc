PROGRAM SRND
  USE, INTRINSIC :: ISO_FORTRAN_ENV, ONLY: ERROR_UNIT
  IMPLICIT NONE
  REAL, PARAMETER :: ZERO = 0.0, HALF = 0.5
  REAL, ALLOCATABLE :: HARVEST(:)
  INTEGER, ALLOCATABLE :: ISEED(:)
  CHARACTER(LEN=64) :: CLA
  INTEGER :: SSIZE, N, I
  LOGICAL :: UPPER

  CALL RANDOM_SEED(SIZE=SSIZE)
  IF (SSIZE .LE. 0) STOP 'seed size non-positive'
  I = COMMAND_ARGUMENT_COUNT()
  IF (I .LT. 2) THEN
     IF (SSIZE .GT. 1) THEN
        WRITE (CLA,'(I1)') SSIZE
        CLA = 'srnd.exe (U[pper]|G[eneral]) N [SEED1 ... SEED'//TRIM(CLA)//']'
     ELSE ! SSIZE = 1
        CLA = 'srnd.exe (U[pper]|G[eneral]) N [SEED1]'
     END IF
     WRITE (ERROR_UNIT,*) CLA
     STOP 'All SEED arguments have to be given, or none of them.'
  END IF
  CALL GET_COMMAND_ARGUMENT(2, CLA)
  READ (CLA,*) N
  IF (N .LT. 0) STOP 'the second argument is invalid'
  CALL GET_COMMAND_ARGUMENT(1, CLA)
  SELECT CASE (CLA(1:1))
  CASE ('U','u')
     UPPER = .TRUE.
  CASE ('G','g')
     UPPER = .FALSE.
  CASE DEFAULT
     STOP 'the first argument is invalid'
  END SELECT
  IF (I .EQ. 2) THEN
     ALLOCATE(ISEED(SSIZE))
     CALL RANDOM_SEED(GET=ISEED)
     DO I = 1, SSIZE
        WRITE (ERROR_UNIT,*) ISEED(I)
     END DO
  ELSE IF (I .EQ. (SSIZE + 2)) THEN
     ALLOCATE(ISEED(SSIZE))
     DO I = 1, SSIZE
        CALL GET_COMMAND_ARGUMENT(I + 2, CLA)
        READ (CLA,*) ISEED(I)
     END DO
     CALL RANDOM_SEED(PUT=ISEED)
  ELSE ! a wrong SEED
     STOP 'invalid number of SEED arguments'
  END IF
  IF (UPPER) THEN
     ALLOCATE(HARVEST(6))
     DO I = 1, N
        CALL RANDOM_NUMBER(HARVEST)
        IF (HARVEST(2) .LT. HALF) HARVEST(1) = -HARVEST(1)
        WRITE (*,1,ADVANCE='NO') ' ', HARVEST(1)
        WRITE (*,1,ADVANCE='NO') ' ', ZERO
        IF (HARVEST(4) .LT. HALF) HARVEST(3) = -HARVEST(3)
        WRITE (*,1,ADVANCE='NO') ' ', HARVEST(3)
        IF (HARVEST(6) .LT. HALF) HARVEST(5) = -HARVEST(5)
        WRITE (*,1) ' ', HARVEST(5)
     END DO
  ELSE ! general
     ALLOCATE(HARVEST(8))
     DO I = 1, N
        CALL RANDOM_NUMBER(HARVEST)
        IF (HARVEST(2) .LT. HALF) HARVEST(1) = -HARVEST(1)
        WRITE (*,1,ADVANCE='NO') ' ', HARVEST(1)
        IF (HARVEST(4) .LT. HALF) HARVEST(3) = -HARVEST(3)
        WRITE (*,1,ADVANCE='NO') ' ', HARVEST(3)
        IF (HARVEST(6) .LT. HALF) HARVEST(5) = -HARVEST(5)
        WRITE (*,1,ADVANCE='NO') ' ', HARVEST(5)
        IF (HARVEST(8) .LT. HALF) HARVEST(7) = -HARVEST(7)
        WRITE (*,1) ' ', HARVEST(7)
     END DO
  END IF
  IF (ALLOCATED(HARVEST)) DEALLOCATE(HARVEST)
  IF (ALLOCATED(ISEED)) DEALLOCATE(ISEED)
1 FORMAT(A,ES16.9E2)
END PROGRAM SRND
