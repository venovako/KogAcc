!>@brief \b DROTC postmultiplies the columns (p,q) of G by W.
PURE SUBROUTINE DROTC(M, N, G, LDG, P, Q, W, INFO)
  USE, INTRINSIC :: ISO_FORTRAN_ENV, ONLY: REAL64
  IMPLICIT NONE

  INTERFACE
     PURE SUBROUTINE DROTM(N, DX, INCX, DY, INCY, DPARAM)
       USE, INTRINSIC :: ISO_FORTRAN_ENV, ONLY: REAL64
       IMPLICIT NONE
       INTEGER, INTENT(IN) :: N, INCX, INCY
       REAL(KIND=REAL64), INTENT(INOUT) :: DX(*), DY(*)
       REAL(KIND=REAL64), INTENT(IN) :: DPARAM(5)
     END SUBROUTINE DROTM
  END INTERFACE

  INTEGER, INTENT(IN) :: M, N, LDG, P, Q
  REAL(KIND=REAL64), INTENT(INOUT) :: G(LDG,N)
  REAL(KIND=REAL64), INTENT(IN) :: W(2,2)
  INTEGER, INTENT(OUT) :: INFO
  REAL(KIND=REAL64) :: PARAM(5)

  INFO = 0
  IF ((Q .LE. P) .OR. (Q .GT. N)) INFO = -6
  IF ((P .LE. 0) .OR. (P .GE. N)) INFO = -5
  IF (LDG .LT. M) INFO = -4
  IF (N .LT. 0) INFO = -2
  IF (M .LT. 0) INFO = -1
  IF (INFO .NE. 0) RETURN
  IF (M .EQ. 0) RETURN
  IF (N .EQ. 0) RETURN

  PARAM(1) = -1.0_REAL64
  PARAM(2) = W(1,1)
  PARAM(3) = W(1,2)
  PARAM(4) = W(2,1)
  PARAM(5) = W(2,2)

  CALL DROTM(M, G(1,P), 1, G(1,Q), 1, PARAM)
END SUBROUTINE DROTC
