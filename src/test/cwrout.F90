!>@brief \b CWROUT writes the BN.UJ, BN.VJ, and BN.SJ files.
SUBROUTINE CWROUT(BN, J, N, U, LDU, V, LDV, S, INFO)
  USE, INTRINSIC :: ISO_FORTRAN_ENV, ONLY: REAL32, REAL128
  IMPLICIT NONE
  INTERFACE
     ELEMENTAL FUNCTION J2C(J)
       IMPLICIT NONE
       INTEGER, INTENT(IN) :: J
       CHARACTER :: J2C
     END FUNCTION J2C
  END INTERFACE
  INTERFACE
     SUBROUTINE BFOPEN(FN, ACT, U, INFO)
       IMPLICIT NONE
       CHARACTER(LEN=*), INTENT(IN) :: FN
       CHARACTER(LEN=2), INTENT(IN) :: ACT
       INTEGER, INTENT(OUT) :: U, INFO
     END SUBROUTINE BFOPEN
  END INTERFACE
  INTERFACE
     SUBROUTINE CBWR2(U, M, N, G, LDG, INFO)
       USE, INTRINSIC :: ISO_FORTRAN_ENV, ONLY: REAL32
       IMPLICIT NONE
       INTEGER, INTENT(IN) :: U, M, N, LDG
       COMPLEX(KIND=REAL32), INTENT(IN) :: G(LDG,N)
       INTEGER, INTENT(OUT) :: INFO
     END SUBROUTINE CBWR2
  END INTERFACE

  CHARACTER(LEN=2), PARAMETER :: ACT = 'WO'
  CHARACTER(LEN=*), INTENT(IN) :: BN
  INTEGER, INTENT(IN) :: J, N, LDU, LDV
  COMPLEX(KIND=REAL32), INTENT(IN) :: U(LDU,N), V(LDV,N)
  REAL(KIND=REAL32), INTENT(IN) :: S(N)
  INTEGER, INTENT(INOUT) :: INFO

  REAL(KIND=REAL128) :: Q
  CHARACTER :: CJ
  INTEGER :: I, K, L

  L = INFO
  INFO = 0
  IF (LDV .LT. N) INFO = -7
  IF (LDU .LT. N) INFO = -5
  IF (N .LT. 0) INFO = -3
  IF ((J .LT. 0) .OR. (J .GT. 63)) INFO = -2
  IF (LEN_TRIM(BN) .LE. 0) INFO = -1
  IF (INFO .NE. 0) RETURN
  IF (N .EQ. 0) RETURN

  CJ = J2C(J)
  CALL BFOPEN(TRIM(BN)//'.U'//CJ, ACT, I, INFO)
  IF (INFO .NE. 0) RETURN
  CALL CBWR2(I, N, N, U, LDU, INFO)
  IF (INFO .NE. 0) RETURN
  CLOSE(I, IOSTAT=INFO)
  IF (INFO .NE. 0) RETURN

  CALL BFOPEN(TRIM(BN)//'.V'//CJ, ACT, I, INFO)
  IF (INFO .NE. 0) RETURN
  CALL CBWR2(I, N, N, V, LDV, INFO)
  IF (INFO .NE. 0) RETURN
  CLOSE(I, IOSTAT=INFO)
  IF (INFO .NE. 0) RETURN

  CALL BFOPEN(TRIM(BN)//'.S'//CJ, ACT, I, INFO)
  IF (INFO .NE. 0) RETURN
  DO K = 1, N
     Q = SCALE(REAL(S(K), REAL128), L)
     WRITE (UNIT=I, IOSTAT=INFO) Q
     IF (INFO .NE. 0) RETURN
  END DO
  CLOSE(I, IOSTAT=INFO)
END SUBROUTINE CWROUT
