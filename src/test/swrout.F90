!>@brief \b SWROUT writes the BN.UJ, BN.VJ, and BN.SJ files.
SUBROUTINE SWROUT(BN, J, N, U, LDU, V, LDV, S, INFO)
  USE, INTRINSIC :: ISO_FORTRAN_ENV, ONLY: REAL32, REAL128
  IMPLICIT NONE
  INTERFACE
     SUBROUTINE BFOPEN(FN, ACT, U, INFO)
       IMPLICIT NONE
       CHARACTER(LEN=*), INTENT(IN) :: FN
       CHARACTER(LEN=2), INTENT(IN) :: ACT
       INTEGER, INTENT(OUT) :: U, INFO
     END SUBROUTINE BFOPEN
  END INTERFACE
  INTERFACE
     SUBROUTINE SBWR2(U, M, N, G, LDG, INFO)
       USE, INTRINSIC :: ISO_FORTRAN_ENV, ONLY: REAL32
       IMPLICIT NONE
       INTEGER, INTENT(IN) :: U, M, N, LDG
       REAL(KIND=REAL32), INTENT(IN) :: G(LDG,N)
       INTEGER, INTENT(OUT) :: INFO
     END SUBROUTINE SBWR2
  END INTERFACE

  CHARACTER(LEN=2), PARAMETER :: ACT = 'WO'
  CHARACTER(LEN=*), INTENT(IN) :: BN
  INTEGER, INTENT(IN) :: J, N, LDU, LDV
  REAL(KIND=REAL32), INTENT(IN) :: U(LDU,N), V(LDV,N), S(N)
  INTEGER, INTENT(INOUT) :: INFO

  REAL(KIND=REAL128) :: Q
  CHARACTER(LEN=2) :: CJ
  INTEGER :: I, K, L

  L = INFO
  INFO = 0
  IF (LDV .LT. N) INFO = -7
  IF (LDU .LT. N) INFO = -5
  IF (N .LT. 0) INFO = -3
  IF ((J .LT. 0) .OR. (J .GT. 63)) INFO = -2
  IF (LEN_TRIM(BN) .LE. 0) INFO = -1
  IF (INFO .NE. 0) RETURN
  IF (N .EQ. 0) RETURN

  CJ = '  '
  IF (J .LT. 10) THEN
     WRITE (CJ,'(I1)') J
  ELSE ! J .GE. 10
     WRITE (CJ,'(I2)') J
  END IF
  CALL BFOPEN(TRIM(BN)//'.U'//TRIM(CJ), ACT, I, INFO)
  IF (INFO .NE. 0) RETURN
  CALL SBWR2(I, N, N, U, LDU, INFO)
  IF (INFO .NE. 0) RETURN
  CLOSE(I, IOSTAT=INFO)
  IF (INFO .NE. 0) RETURN

  CALL BFOPEN(TRIM(BN)//'.V'//CJ, ACT, I, INFO)
  IF (INFO .NE. 0) RETURN
  CALL SBWR2(I, N, N, V, LDV, INFO)
  IF (INFO .NE. 0) RETURN
  CLOSE(I, IOSTAT=INFO)
  IF (INFO .NE. 0) RETURN

  CALL BFOPEN(TRIM(BN)//'.S'//CJ, ACT, I, INFO)
  IF (INFO .NE. 0) RETURN
  DO K = 1, N
     Q = SCALE(REAL(S(K), REAL128), L)
     WRITE (UNIT=I, IOSTAT=INFO) Q
     IF (INFO .NE. 0) RETURN
  END DO
  CLOSE(I, IOSTAT=INFO)
END SUBROUTINE SWROUT
